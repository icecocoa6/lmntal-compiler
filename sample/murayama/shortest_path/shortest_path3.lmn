init({
	thread.create(4).

	/* ノードをファイルから読出し */
	rd(0)=io.fileReader([:testfile_nodes4:]).
	rd(X,O) :- class(O,"java.io.BufferedReader"),X<202 | rd(X+1,O), readnode(O).
	
	readnode(Object) :- class(Object, "java.io.BufferedReader") |
		[:/*inline*/
			try {
				java.io.BufferedReader br = (java.io.BufferedReader) ((ObjectFunctor)me.nthAtom(0).getFunctor()).getObject();
				String s = br.readLine();
			 	String r[] = s.split(" ");
	  			Atom e = mem.newAtom(new Functor("p", 2));
				Atom result1 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[0])));
				Atom result2 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[1])));
	
				mem.newLink(e, 0, result1, 0);
				mem.newLink(e, 1, result2, 0);
				
				me.nthAtom(0).remove();
				me.remove();
			} catch(Exception e) {Env.e(e);}
		:](Object).
	startpoint(32).
	p(X,Y), startpoint(ST) :- X==ST,  int(Y) | startpoint(ST), p(X, {}, 0, Y).
	p(X,Y), startpoint(ST) :- X=\=ST, int(Y) | startpoint(ST), p(X, {}, 100000, Y).
	
	/* リンクをファイルから読出し */
	r(0)=io.fileReader([:testfile_edges4:]).
	r(X,O) :- class(O,"java.io.BufferedReader"),X<303 | r(X+1,O), readlink(O).
	
	readlink(Object) :- class(Object, "java.io.BufferedReader") |
		[:/*inline*/
			try {
				java.io.BufferedReader br = (java.io.BufferedReader) ((ObjectFunctor)me.nthAtom(0).getFunctor()).getObject();
				String s = br.readLine();
			 	String r[] = s.split(" ");
	  			Atom e = mem.newAtom(new Functor("e", 4));
				Atom result1 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[0])));
				Atom result2 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[1])));
				Atom result3 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[2])));
				Atom result4 = mem.newAtom(new IntegerFunctor(Integer.parseInt(r[3])));
	
				mem.newLink(e, 0, result1, 0);
				mem.newLink(e, 1, result2, 0);
				mem.newLink(e, 2, result3, 0);
				mem.newLink(e, 3, result4, 0);
				
				me.nthAtom(0).remove();
				me.remove();
			} catch(Exception e) {Env.e(e);}
		:](Object).
	
	/* ノードとリンクをつなぐ */
	connect1@@
	p(X1, {$p}, W, Thread1), e(X2,Y2,Z, Thread2)
		:- X1==X2, int(Thread1), int(Thread2) |
			p(X1, {+(X), $p}, W, Thread1), e(X,Y2,Z, Thread2).
	connect2@@
	p(Y1, {$p}, W, Thread1), e(X2,Y2,Z, Thread2)
		:- Y1==Y2, int(Thread1), int(Thread2) |
			p(Y1, {+(Y), $p}, W, Thread1), e(X2,Y,Z, Thread2).
}).

/* スレッド割り当て */
scatter({
	scatter_node@@
	p(X, {$p}, Weight, Thread) :- int(X), int(Weight), Thread=\=100 |
		thread.at(Thread, {{$p, w(X, Weight)}}).
	scatter_edge@@
	e(X,Y,Weight,Thread) :- int(Weight), Thread=\=100 |
		thread.at(Thread, {e(X,Y,Weight)}).
}).

/* Moore-Bellman-Fordのアルゴリズムによる最短経路探索 */
mbf({
	go :-
		started,
		thread.all({
			inner_mbf1@@
			e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, {+(V2), w(X2, PoB), $p2}
				:- PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
					e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, {+(V2), w(X2, PoB2), $p2}.
			inner_mbf2@@
			e(V2, V1, Weight), {+(V1), w(X1, PoA), $p1}, {+(V2), w(X2, PoB), $p2}
				:- PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
					e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, {+(V2), w(X2, PoB2), $p2}.
					}).

	outer_mbf1@@
 	started
 	, thread(X,{e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, $p, @p})
 	, thread(Y, {{+(V2), w(X2, PoB), $p2}, $q, @q})
 		:- int(X), int(Y), PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
 			started
 			, thread(X,{e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, $p, @p})
 			, thread(Y,{{+(V2), w(X2, PoB2), $p2}, $q, @q}).
	outer_mbf2@@
 	started
 	, thread(X,{{+(V1), w(X1, PoA), $p1}, $p, @p})
 	, thread(Y, {e(V1, V2, Weight), {+(V2), w(X2, PoB), $p2}, $q, @q})
 		:- int(X), int(Y), PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
 			started
 			, thread(X,{{+(V1), w(X1, PoA), $p1}, $p, @p})
 			, thread(Y,{e(V1, V2, Weight), {+(V2), w(X2, PoB2), $p2}, $q, @q}).

	outer_mbf3@@
 	started
 	, thread(X,{e(V2, V1, Weight), {+(V1), w(X1, PoA), $p1}, $p, @p})
 	, thread(Y, {{+(V2), w(X2, PoB), $p2}, $q, @q})
 		:- int(X), int(Y), PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
 			started
 			, thread(X,{e(V1, V2, Weight), {+(V1), w(X1, PoA), $p1}, $p, @p})
 			, thread(Y,{{+(V2), w(X2, PoB2), $p2}, $q, @q}).

	outer_mbf4@@
 	started
 	, thread(X,{{+(V1), w(X1, PoA), $p1}, $p, @p})
 	, thread(Y, {e(V2, V1, Weight), {+(V2), w(X2, PoB), $p2}, $q, @q})
 		:- int(X), int(Y), PoA + Weight < PoB, int(X1), int(X2), PoB2 = PoA + Weight |
 			started
 			, thread(X,{{+(V1), w(X1, PoA), $p1}, $p, @p})
 			, thread(Y,{e(V1, V2, Weight), {+(V2), w(X2, PoB2), $p2}, $q, @q}).
}).

init({$p, @p}/), scatter({@q}) :- scattering({$p,@p,@q}).
scattering({$p, @p}/), mbf({@q}) :- timer:benchmark(X,"Elapsed Time"), mbfing({+(X),$p,@p,@q}).
