// --wtモードで実行してみてください。メモ帳です。
// デフォルトでC:\text.txtに保存されます。右上の部分変えれば保存先変わる。


{
	sys.perpetual(on).
	window.
	name("TextEditor   by LMNtal").
	size(400,300).
	killer.
	{ id(open), button, text("open"), position(0,0), weight(1.0, 0)}.
	{ id(save), button, text("save"), position(1,0), weight(1.0, 0)}.
	{ id(file), textarea, text("C:\\test.txt"), position(2,0), size(1,1), weight(9.0, 0)}.
	{ id(txt),  textarea, text(""), position(0,1), size(3,1), weight(1.0, 1.0) }.
	
	//****以下ルール****// 
	
	save1@@
	{ id(save), clicked, $p, @p}, { id(file), text(_SFILE), $q, @q } :- 
	{ id(save),          $p, @p}, { id(file), text(_SFILE), $q, @q },
	writer=io.fileWriter(_SFILE).
	
	save2@@
	writer=WRITER,                  { id(txt), text(_STEXT), $p, @p} :-
	print=io.print(WRITER, _STEXT), { id(txt), text(_STEXT), $p, @p}.
	
	open1@@
	{ id(open), clicked, $p, @p}, { id(file), text(_SFILE), $q, @q } :- 
	{ id(open),          $p, @p}, { id(file), text(_SFILE), $q, @q },
	reader=io.fileReader(_SFILE).
	
	open2@@
	reader=READER :-
	read=io.readall(READER, readRes).
	
	open3@@
	readRes=_SRES, { id(txt), text(_STEXT), $p, @p} :-
	               { id(txt), text(_SRES),  $p, @p}.
	
	close1@@
	done=CLOSE :- close=io.close(CLOSE).
	
	close2@@
	close=_GCLOSE :-.
}.

