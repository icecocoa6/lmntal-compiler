//dna = ['A','T','G','G','A','A','T','T','C','T','C','G','C','T','C'].
dna = ['A','T','C','G','G'].

 {
  //アトム名"a"をサイズ0.5に指定
  chorus.atomSize(c, 0.35).
  chorus.atomSize(n, 0.4).
  chorus.atomSize(h, 0.2).
  chorus.atomSize(o, 0.45).
  chorus.atomSize(p, 0.6).
  //"a"と"b"の間に張られるリンクの長さを1.5に指定
  // chorus.linkLength(b, a, 1.5)でも同義
  chorus.linkLength(c, c, 0.77).
  chorus.linkLength(c, n, 0.735).
  chorus.linkLength(c, o, 0.715).
  chorus.linkLength(n, n, 0.725).
  chorus.linkLength(n, o, 0.68).
  chorus.atomColor(c, 0.5, 0.5, 0.5).
  chorus.atomColor(n, 0.0, 0.0, 1.0).
  chorus.atomColor(h, 1.0, 1.0, 1.0).
  chorus.atomColor(o, 1.0, 0.0, 0.0).
  chorus.atomColor(p, 1.0, 0.0, 1.0).
 }.
/* DNAの分子構造をグラフ構造で表現するプログラム
 * ルールの反応によってDNAができていくが、
 * このルールは化学反応を示しているわけではない
 * 原子同士はリンクで繋がって分子構造を示すが、
 * 二重結合は一本のリンクで表現している
 */

//DNAは二本鎖による螺旋構造を持っているので、それぞれの鎖を定義する
strand({
  //反応の起点となる最初のデオキシリボース
  //反応の起点であるということを示すため、臨時に"act"というアトムをつけている
  o(A,B),c(B,o(h),h,E),c(E,h,h,H),c(H,o(h,act),h,K),c(K,c(h,h,o(h)),h,A).
}).
strand({
  o(A,B),c(B,o(h),h,E),c(E,h,h,H),c(H,o(h,act),h,K),c(K,c(h,h,o(h)),h,A).
}).

//それぞれの鎖を構成させるためのルール群
rules({
  //1個目のデオキシリボースは既にあるため、作成する必要はない。
  remove, makeDeoxyribose :- removed.

  //デオキシリボースを作成する
  //反応の起点でないことを示すため、臨時に"pas"というアトムをつけている
  makeDeoxyribose@@
  removed, makeDeoxyribose :-
    removed,
    o(A,B),c(B,o(h),h,E),c(E,h,h,H),c(H,o(h,pas),h,K),c(K,c(h,h,o(h)),h,A).
  
  //２つのデオキリボースを、間にリン酸を挟んで繋げる
  //反応の起点を示す"act"をもう一方に渡していくことで、
  //デオキシリボースが円のように繋がらないようにする
  //Link1：上側にあるデオキシリボースとリン酸をつなげるリンク
  //Link2：下側にあるデオキシリボースとリン酸をつなげるリンク
  makeFrame@@
  o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(h,act),J1,K1),c(K1,L1,M1,A1),
  o(A2,B2),c(B2,C2,D2,E2),c(E2,F2,G2,H2),c(H2,o(h,pas),J2,K2),c(K2,c(X2,X3,o(h)),M2,A2) :-
    o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(Link2,pas),J1,K1),c(K1,L1,M1,A1),
    o(A2,B2),c(B2,C2,D2,E2),c(E2,F2,G2,H2),c(H2,o(h,act),J2,K2),c(K2,c(X2,X3,o(Link1)),M2,A2),
    p(Link1,o,o(h),Link2).
  
  //アデニンを作成する
  makeAdenine@@
  makeAdenine :-  
    c(A,n(h,h),C),c(C,D,E),c(E,F,G),n(G,H),c(H,h,J),n(J,h,A),
          n(D,K),c(K,h,M),n(M,h,F).
  
  //グアニンを作成する
  makeGuanine@@
  makeGuanine :- 
    c(A,o,C),c(C,D,E),c(E,F,G),n(G,H),c(H,n(h,h),J),n(J,h,A),
          n(D,K),c(K,h,M),n(M,h,F).

  //チミンを作成する  
  makeThymine@@
  makeThymine :- 
    c(A,o,C),c(C,c(h,h,h),E),c(E,h,G),n(G,h,I),c(I,o,K),n(K,h,A).
  
  //シトシンを作成する
  makeCytosine@@
  makeCytosine :-
    c(A,n(h,h),C),c(C,h,E),c(E,h,G),n(G,h,H),c(H,o,I),n(I,h,A).
  
  //アデニンまたはグアニンを、デオキシリボースと繋げる
  //アデニンとグアニンは構造が似ているため、1つのルールで表現できた
  //Link：アデニンまたはグアニンと、デオキシリボースが繋がっているリンク
  AGwithDeoxyribose@@
  o(A,B),c(B,o(h),D,E),c(E,F,G,H),c(H,I,J,K),c(K,L,M,A),
  c(A2,B2,C2),c(C2,D2,E2),c(E2,F2,G2),n(G2,H2),c(H2,I2,J2),n(J2,K2,A2),
  n(D2,L2),c(L2,M2,N2),n(N2,h,F2) :-
    o(A,B),c(B,o(Link),D,E),c(E,F,G,H),c(H,I,J,K),c(K,L,M,A),
    c(A2,B2,C2),c(C2,D2,E2),c(E2,F2,G2),n(G2,H2),c(H2,I2,J2),n(J2,K2,A2),
    n(D2,L2),c(L2,M2,N2),n(N2,Link,F2).

  //シトシンまたはチミンを、デオキシリボースと繋げる
  //シトシンとチミンは構造が似ているため、1つのルールで表現できた
  //Link：シトシンまたはチミンと、デオキシリボースが繋がっているリンク
  CTwithDeoxyribose@@
  o(A,B),c(B,o(h),D,E),c(E,F,G,H),c(H,I,J,K),c(K,L,M,A),
  c(A2,B2,C2),c(C2,D2,E2),c(E2,F2,G2),n(G2,h,I2),c(I2,J2,K2),n(K2,L2,A2) :-
    o(A,B),c(B,o(Link),D,E),c(E,F,G,H),c(H,I,J,K),c(K,L,M,A),
    c(A2,B2,C2),c(C2,D2,E2),c(E2,F2,G2),n(G2,Link,I2),c(I2,J2,K2),n(K2,L2,A2).
}).

//二本の鎖を水素結合させるためのルール群１
//N個の塩基があったら、1番目〜N-1番目の塩基同士を結合させる
hydrogenBond({
  //アデニンとチミンを水素結合させる
  //共有結合と同様にリンクで繋ぐことで表現している
  //対応する配列同士で結合するように、"act"を次々と回していく
  //1-5行目はアデニン側で、1,2行目がデオキシリボースと3行目がリン酸、
  //4,5行目がアデニン
  //6-9行目はチミン側で、6,7行目がデオキシリボースと8行目がリン酸、
  //9行目がチミン
  //Link1, Link2：アデニンとチミンの水素結合
  hydrogenBondAT@@
  o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(Li2,pas),J1,K1),c(K1,L1,M1,A1),
  o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,act),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
  p(Li1,o,o(h),Li2),
  c(A,n(h,h),C),c(C,D,E),c(E,F,G),n(G,H),c(H,I,J),n(J,h,A),
  n(D,K),c(K,L,M),n(M,Lin1,F),
  o(A3,B3),c(B3,C3,D3,E3),c(E3,F3,G3,H3),c(H3,o(Li4,pas),J3,K3),c(K3,L3,M3,A3),
  o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,act),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
  p(Li3,o,o(h),Li4),
  c(A5,o,C5),c(C5,D5,E5),c(E5,h,G5),n(G5,Lin2,I5),c(I5,J5,K5),n(K5,h,A5) :-
    o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(Li2,act),J1,K1),c(K1,L1,M1,A1),
    o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,pas),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
    p(Li1,o,o(h),Li2),
    c(A,n(h,h(Link1)),C),c(C,D,E),c(E,F,G),n(G,H),c(H,I,J),n(J,Link2,A),
    n(D,K),c(K,L,M),n(M,Lin1,F),
    o(A3,B3),c(B3,C3,D3,E3),c(E3,F3,G3,H3),c(H3,o(Li4,act),J3,K3),c(K3,L3,M3,A3),
    o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,pas),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
    p(Li3,o,o(h),Li4),
    c(A5,Link1,C5),c(C5,D5,E5),c(E5,h,G5),n(G5,Lin2,I5),c(I5,J5,K5),n(K5,h(Link2),A5).
  
  //シトシンとグアニンを水素結合させる
  //共有結合と同様にリンクで繋ぐことで表現している
  //対応する配列同士で結合するように、"act"を次々と回していく
  //1-5行目はグアニン側で、1,2行目がデオキシリボースと3行目がリン酸、4,5行目がグアニン
  //6-9行目はシトシン側で、6,7行目がデオキシリボースと8行目がリン酸、9行目がシトシン
  //Link1, Link2, Link3：アデニンとチミンの水素結合  
  hydrogenBondCG@@
  o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(Li2,pas),J1,K1),c(K1,L1,M1,A1),
  o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,act),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
  p(Li1,o,o(h),Li2),
  c(A,o,C),c(C,D,E),c(E,F,G),n(G,H),c(H,n(h,h),J),n(J,h,A),
  n(D,K),c(K,L,M),n(M,Lin1,F),
  o(A3,B3),c(B3,C3,D3,E3),c(E3,F3,G3,H3),c(H3,o(Li4,pas),J3,K3),c(K3,L3,M3,A3),
  o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,act),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
  p(Li3,o,o(h),Li4),
  c(A5,n(h,h),C5),c(C5,D5,E5),c(E5,F5,G5),n(G5,Lin2,I5),c(I5,o,K5),n(K5,h,A5) :-
    o(A1,B1),c(B1,C1,D1,E1),c(E1,F1,G1,H1),c(H1,o(Li2,act),J1,K1),c(K1,L1,M1,A1),
    o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,pas),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
    p(Li1,o,o(h),Li2),
    c(A,o(Link1),C),c(C,D,E),c(E,F,G),n(G,H),c(H,n(h,h(Link3)),J),n(J,h(Link2),A),
    n(D,K),c(K,L,M),n(M,Lin1,F),
    o(A3,B3),c(B3,C3,D3,E3),c(E3,F3,G3,H3),c(H3,o(Li4,act),J3,K3),c(K3,L3,M3,A3),
    o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,pas),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
    p(Li3,o,o(h),Li4),
    c(A5,n(h,h(Link1)),C5),c(C5,D5,E5),c(E5,F5,G5),n(G5,Lin2,I5),c(I5,o(Link3),K5),n(K5,Link2,A5).
}).

//二本の鎖を水素結合させるためのルール群２
//N個の塩基があったら、N番目の塩基同士を結合させる
hydrogenBondLast({
  //アデニンとチミンを水素結合させる
  //共有結合と同様にリンクで繋ぐことで表現している
  //最後に位置するデオキシリボースは下側にリン酸を持たないため、別ルールで表す
  //1-3行目はアデニン側で、1行目がデオキシリボース、2,3行目がアデニン
  //4-5行目はチミン側で、4行目がデオキシリボース、5行目がチミン
  //Link1, Link2：アデニンとチミンの水素結合
  hydrogenBondATlast@@
  o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,act),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
  c(A,n(h,h),C),c(C,D,E),c(E,F,G),n(G,H),c(H,I,J),n(J,h,A),
  n(D,K),c(K,L,M),n(M,Lin1,F),
  o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,act),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
  c(A5,o,C5),c(C5,D5,E5),c(E5,h,G5),n(G5,Lin2,I5),c(I5,J5,K5),n(K5,h,A5) :-
    o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,pas),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
    c(A,n(h,h(Link1)),C),c(C,D,E),c(E,F,G),n(G,H),c(H,I,J),n(J,Link2,A),
    n(D,K),c(K,L,M),n(M,Lin1,F),
    o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,pas),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
    c(A5,Link1,C5),c(C5,D5,E5),c(E5,h,G5),n(G5,Lin2,I5),c(I5,J5,K5),n(K5,h(Link2),A5).
  
  //シトシンとグアニンを水素結合させる
  //共有結合と同様にリンクで繋ぐことで表現している
  //最後に位置するデオキシリボースは下側にリン酸を持たないため、別ルールで表す
  //1-3行目はグアニン側で、1行目がデオキシリボース、2,3行目がグアニン
  //4-5行目はシトシン側で、4行目がデオキシリボース、5行目がシトシン
  //Link1, Link2, Link3：アデニンとチミンの水素結合  
  hydrogenBondCGlast@@
  o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,act),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
  c(A,o,C),c(C,D,E),c(E,F,G),n(G,H),c(H,n(h,h),J),n(J,h,A),
  n(D,K),c(K,L,M),n(M,Lin1,F),
  o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,act),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
  c(A5,n(h,h),C5),c(C5,D5,E5),c(E5,F5,G5),n(G5,Lin2,I5),c(I5,o,K5),n(K5,h,A5) :-
    o(A2,B2),c(B2,o(Lin1),D2,E2),c(E2,F2,G2,H2),c(H2,o(AC1,pas),J2,K2),c(K2,c(X2,X3,o(Li1)),M2,A2),
    c(A,o(Link1),C),c(C,D,E),c(E,F,G),n(G,H),c(H,n(h,h(Link3)),J),n(J,h(Link2),A),
    n(D,K),c(K,L,M),n(M,Lin1,F),
    o(A4,B4),c(B4,o(Lin2),D4,E4),c(E4,F4,G4,H4),c(H4,o(AC2,pas),J4,K4),c(K4,c(X4,X5,o(Li3)),M4,A4),
    c(A5,n(h,h(Link1)),C5),c(C5,D5,E5),c(E5,F5,G5),n(G5,Lin2,I5),c(I5,o(Link3),K5),n(K5,Link2,A5).
}).

//リスト構造で表現された塩基構造を元に、
//塩基とデオキリボースをそれぞれの鎖に作っていく
//一方の配列は与えられた塩基を、他方の配列はその塩基に対応する塩基を作成する
L = ['A' | R], strand1({$p,@p}/), strand2({$q,@q}/) :-
   L = R,
   strand1({makeDeoxyribose, makeAdenine, $p,@p}),
   strand2({makeDeoxyribose, makeThymine, $q,@q}).
L = ['T' | R], strand1({$p,@p}/), strand2({$q,@q}/) :-
   L = R,
   strand1({makeDeoxyribose, makeThymine, $p,@p}),
   strand2({makeDeoxyribose, makeAdenine, $q,@q}).
L = ['C' | R], strand1({$p,@p}/), strand2({$q,@q}/) :-
   L = R,
   strand1({makeDeoxyribose, makeCytosine, $p,@p}),
   strand2({makeDeoxyribose, makeGuanine, $q,@q}).
L = ['G' | R], strand1({$p,@p}/), strand2({$q,@q}/) :-
   L = R,
   strand1({makeDeoxyribose, makeGuanine, $p,@p}),
   strand2({makeDeoxyribose, makeCytosine, $q,@q}).

//二本の鎖に、それぞれの鎖を構成させるためのルール群を渡す
//鎖には既に１つデオキシリボースがあるので、
//最初に渡されたものを削除することを示すremoveアトムも一緒に渡す
strand({$p}), strand({$q}), rules({@p}) :-
  strand1({remove, $p,@p}), strand2({remove, $q,@p}).

//二本の鎖の構成が終了したら、それを水素結合させるルールに渡す
dna([]), strand1({removed, $p,@p}/), strand2({removed, $q,@q}/), 
hydrogenBond({@r}) :-
   hydrogenBond({$q,$p,@p,@r}).

//それが終わったら、最後の水素結合をさせる
hydrogenBond({$p,@p}/), hydrogenBondLast({@q}) :-
   hydrogenBondLast({$p,@p,@q}).

//水素結合が終わったら、膜の外に出す
hydrogenBondLast({$p,@p}/) :- $p,@p.

//臨時につけておいた"pas"アトムは削除する(actアトムは削除済み)
o(X,pas,Y) :- o(X,Y).
