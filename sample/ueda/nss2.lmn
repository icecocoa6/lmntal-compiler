/*
 * simplified Needham-Schroeder
 * 2007/12/27(Thu) by okabe
 *
 * A->B : Na
 * B->A : Na,Nb
 * A->B : Nb
 *
 * @see
 *   I. Cervesato, N.A. Durgin, P.D. Lincoln, J.C. Mitchell, A. Scedrov.
 *   A Meta-notation for Protocol Analysis, 1999
 */

% system rule
t0_init{

a0, b0.
snd1(1), snd2(1), gen(1).

/* simplified Needham-Schroeder */
a0 :-
    n1(A0), a1(A1), {+A0,+A1}.
b0, n1(A0), {+A0,$a} :-
    n2(A1,B1), b1(A2,B2), {+A1,+A2,$a}, {+B1,+B2}.
n2(A0,B0), a1(A1), {+A0,+A1,$a}, {+B0,$b} :-
    n3(B1), a2(A2,B2), {+A2,$a}, {+B1,+B2,$b}.
n3(B0), b1(A0,B1), {+A0,$a}, {+B0,+B1,$b} :-
    b2(A1,B2), {+A1,$a}, {+B2,$b}.

% intruder
n1(X) :- m(X).
/* snd1(N1),*/
m(X), {+X,$x} :- /*N1>0, N2=N1-1 |*/
    /* snd1(N2),*/ n1(X1), m(X2), {+X1,+X2,$x}.
n2(X,Y) :- m(X), m(Y).
n3(X) :- m(X).

snd2(N1), m(X), m(Y),
    {+X,$x}, {+Y,$y} :- N1>0, N2=N1-1 |
    snd2(N2), n(X1,Y1), m(X2), m(Y2),
    {+X1,+X2,$x}, {+Y1,+Y2,$y}.

gen(N1) :- N1>0, N2=N1-1 | gen(N2), m(N), {+N}.

}.

% property rule
ltl_1@@
t0_init{$p,@p} :- t0_init{$p,@p}.
ltl_2@@
t0_init{ m(I1),m(I2),m(I3),a2(A1,A2),b2(B1,B2),
  {+I1,+B1,$1},{+I3,+A1,$2},{+I2,+A2,+B2,$3},$p,@p } :-
accept_all_end{ m(I1),m(I2),m(I3),a2(A1,A2),b2(B1,B2),
  {+I1,+B1,$1},{+I3,+A1,$2},{+I2,+A2,+B2,$3},$p,@p }.

/* ltl2ba output (never claim)
> ~/workspace/mc/ltl2ba-1.0/ltl2ba -f '!([]!(p))'
never {
T0_init:
        if
        :: (1) -> goto T0_init
        :: (p) -> goto accept_all
        fi;
accept_all:
        skip
}
*/

/* literal definition
p@@
{ m(I1),m(I2),m(I3),l2(a,A1,A2),l2(b,B1,B2),
  {+I1,+B1,$1},{+I3,+A1,$a},{+I2,+A2,+B2,$2},$p,@p } :-
{ m(I1),m(I2),m(I3),l2(a,A1,A2),l2(b,B1,B2),
  {+I1,+B1,$1},{+I3,+A1,$a},{+I2,+A2,+B2,$2},$p,@p }.
*/
