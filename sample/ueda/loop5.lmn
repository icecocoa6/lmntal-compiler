input{
   arrow(a,b). arrow(b,c). arrow(c,c). arrow(a,d). arrow(c,a). arrow(c,b).
}.

factory{
   arrows{{start(a)}, {start(b)}, {start(c)}, {start(d)}}.
   paths{
      path([$x|$xs]) :- unary($x), ground($xs) | p2(member($x,$xs),[$x|$xs]).
         p2(false,        Xs) :- nonloop(Xs).
         p2(true_nonfinal,Xs) :- lasso_rev(Xs).
         p2(true_final,   Xs) :- loop_rev(Xs,[]).
           loop_rev([],    Ys) :- loop(Ys).
           loop_rev([X|Xs],Ys) :- loop_rev(Xs,[X|Ys]).

      member($x,[],          Res) :- unary($x) | Res=false.
      member($x,[$y],        Res) :- $x==$y | Res=true_final.
      member($x,[$y,$y2|$ys],Res) :- $x==$y, unary($y2), ground($ys) |
         Res=true_nonfinal.
      member($x,[$y|Ys],     Res) :- $x\=$y | member($x,Ys,Res).
   }.

   doing($x0), arrows{{arrow($x,$y),$p},$s}, paths{$q,@q} :-
      $x0==$x, uniq($x,$y) |
      doing($x0), arrows{{arrow($x,$y),$p},$s}, paths{path([$y,$x]),$q,@q}.
   arrows{{arrow($x0,$y),$p},$s}, paths{nonloop([$x|$xs]),$q,@q} :-
      $x0==$x, uniq($y,$x,$xs) |
      arrows{{arrow($x0,$y),$p},$s}, 
      paths{nonloop([$x|$xs]), path([$y,$x|$xs]),$q,@q}.
}.

input{arrow($x0,$y),$p}, factory{arrows{{start($x),$q},$s},$r,@r} :-
   $x0==$x, unary($y) |
   input{$p}, factory{arrows{{start($x),arrow($x0,$y),$q},$s},$r,@r}.

input{}, factory{$q, @q} :- factory{pausing, $q, @q}.
factory{pausing, arrows{}, paths{$p,@p},@q}/ :-
   output{ $p, (nonloop(L):-ground(L)|), (lasso_rev(L):-ground(L)|) }.
factory{pausing, arrows{{start($x),$p},$s}, $q, @q} :- unary($x) |
   factory{doing($x), arrows{{start($x),$p},$s}, $q, @q}.
factory{doing($x), arrows{{start($y),$p[]},$s}, $q, @q}/ :- $x==$y |
   factory{pausing, arrows{$s}, $q, @q}.  // discards $p[]
