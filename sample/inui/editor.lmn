sys.perpetual(on).

java_awt_Component.use.
java_awt_Container.use.
java_awt_TextComponent.use.
java_lang_String.use.
java_awt_event_ActionEvent.use.

//フレームを生成
frame=add(setSize(java_awt_Frame.new("LMNtal Editor"), 600, 400, r1), "Center",
	setFont(java_awt_TextArea.new, java_awt_Font.new("Monospaced", java_awt_Font.plain, 12), r2),
	ta).
//メニューバーを生成
exititem=addActionListener(java_awt_MenuItem.new("Exit"), java_awt_event_ActionListener.new, r).
openitem=addActionListener(java_awt_MenuItem.new("Open"), java_awt_event_ActionListener.new, r).
saveitem=addActionListener(java_awt_MenuItem.new("Save"), java_awt_event_ActionListener.new, r).
searchitem=addActionListener(java_awt_MenuItem.new("Search"), java_awt_event_ActionListener.new, r).

exititem(Exit), openitem(Open), saveitem(Save) :-
	filemenu=add(add(add(java_awt_Menu.new("File"), Open, r), Save, r), Exit, r).
searchitem(Search) :-
	helpmenu=add(java_awt_Menu.new("Search"), Search, r).

filemenu(FileMenu), helpmenu(HelpMenu) :- menubar=add(add(java_awt_MenuBar.new, FileMenu, kill), HelpMenu, kill).

frame(Frame), menubar(MenuBar) :- frame=setVisible(setMenuBar(Frame, MenuBar, r5), true, r7).
frame(Frame), r7(done) :- fd=java_awt_FileDialog.new(Frame).

//アクション
actionPerformed(ActionEvent) :- kill=getActionCommand(ActionEvent, cmd).

cmd("Open"), fd(FD) :- fd=getFile(setVisible(FD, true, open1), openfile).
open1(done), openfile(File), ta(TA) :- ta=setText(TA, io.gets(File), r).

cmd("Save"), fd(FD) :- fd=getFile(setVisible(FD, true, open2), savefile).
open2(done), ta(TA) :- ta=getText(TA, contents).
savefile(File), contents(C) :- string(C) | r=io.puts(File, C).

cmd("Search"), fd(FD) :- fd=getFile(setVisible(FD, true, open3), search).
open3(done), ta(TA) :- ta=getText(TA, s).
s(S), search(Search) :- s=indexOf(S, length(Search, len), index).
index(Index), len(Len), ta(TA) :- int(Index), int(Len) | ta=requestFocus(select(TA, Index, Index+Len, r), r).

cmd("Exit") :- r=java_lang_system.exit(0).

kill(X) :- unary(X) | .