/*
 * swing 版テキストエディタ
 * 要 java_lib
 */
sys.perpetual(on).

java_lang_String.use.
java_awt_event_ActionEvent.use.
java_io_File.use.
javax_swing_JViewport.use.

/////////////////////////////////////////////
// GUI の生成

//メニューバーを生成
SearchMenu=add(javax_swing_JMenu.new("Search"), item("Search"), gc).
FileMenu=add(add(add(add(add(add(add(add(add(
	javax_swing_JMenu.new("File"),
	item("New"), gc), item("Open"), gc), item("Close"), gc), item("Close All"), gc),
	javax_swing_JSeparator.new, gc),
	item("Save"), SaveItem), item("Save as"), gc),
	javax_swing_JSeparator.new, gc),
	item("Exit"), gc).
EditMenu=add(add(add(add(add(add(add(
	javax_swing_JMenu.new("Edit"),
	item("Copy"), gc), item("Cut"), gc), item("Paste"), gc), item("Select All"), gc),
	javax_swing_JSeparator.new, gc),
	item("to Upper Case"), gc), item("to Lower Case"), gc).
MenuBar=add(add(add(
	javax_swing_JMenuBar.new,
	FileMenu, gc), EditMenu, gc), SearchMenu, gc).
//ActionListener つきの MenuItem を生成するルール
H=item(Label) :-
	H=addActionListener(javax_swing_JMenuItem.new(Label), java_awt_event_ActionListener.new, r).

saveItem=setEnabled(SaveItem, false, r).

//タブを生成
Tab=javax_swing_JTabbedPane.new.

//フレームを生成
frame=setVisible(setDefaultCloseOperation(setJMenuBar(getContentPane(setSize(
	javax_swing_JFrame.new("LMNtal Tab Editor"),
	600, 400, r), ContentPane), MenuBar, r), javax_swing_JFrame.exit_on_close, r), true, r).
gc=add(ContentPane, "Center", Tab, tab).

//ファイル選択ダイアログを生成
fc=javax_swing_JFileChooser.new.

/////////////////////////////////////////////
// アクションの定義

//アクションイベントからコマンド名を取得する
actionPerformed(ActionEvent) :- gc=getActionCommand(ActionEvent, cmd).

cmd("New"), tab(Tab) :-
	Scroll=javax_swing_JScrollPane.new(TextArea),
	tab=addTab(Tab, "", Scroll, r),
	TextArea=setFont(javax_swing_JTextArea.new, java_awt_Font.new("Monospaced", java_awt_Font.plain, 14), r).

cmd("Open"), fc(FC), frame(Frame) :- class(Frame, "javax.swing.JFrame") |
	fc=showOpenDialog(FC, Frame, res_open), frame(Frame).
res_open(0), tab(Tab), fc(FC), saveItem(SaveItem) :-
	Scroll=javax_swing_JScrollPane.new(TextArea),
	tab=addTab(Tab, Title, Scroll, r),
	saveItem=setEnabled(SaveItem, true, r),
	fc=getSelectedFile(FC, X),
	gc=getAbsolutePath(getAbsolutePath(getAbsolutePath(X, File), currentFile), Title),
	TextArea=setText(setFont(
		javax_swing_JTextArea.new,
		java_awt_Font.new("Monospaced", java_awt_Font.plain, 14), r), io.gets(File), r).

cmd("Save"), tab(Tab) :-
	tab=getSelectedComponent(getTitleAt(getSelectedIndex(Tab, Index), Index, File), Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=getText(TA, Contents),
	r=io.puts(File, Contents).

cmd("Save as"), fc(FC), frame(Frame) :- class(Frame, "javax.swing.JFrame") |
	fc=showSaveDialog(FC, Frame, res_save), frame(Frame).
res_save(0), fc(FC), tab(Tab) :-
	fc=getSelectedFile(FC, X),
	gc=getAbsolutePath(getAbsolutePath(X, File), Title),
	tab=setTitleAt(getSelectedIndex(getSelectedComponent(Tab, Scroll), Index), Index, Title, r),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=getText(TA, Contents),
	r=io.puts(File, Contents).
	
cmd("Close"), tab(Tab) :-
	tab=remove(getSelectedComponent(Tab, Scroll), Scroll, r).
	
cmd("Close All"), tab(Tab) :-
	tab=removeAll(Tab, r).

cmd("Search") :- word=javax_swing_JOptionPane.showInputDialog("Input word.").
word(Word), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	ta=getCaretPosition(getText(TA, Text), Caret),
	gc=indexOf(Text, length(Word, len), Caret, index).
index(Index), len(Len), ta(TA) :- Start=Index, End=Index+Len | gc=requestFocus(select(TA, Start, End, r), r).

cmd("Copy"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=copy(TA, r).
	
cmd("Cut"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=cut(TA, r).
	
cmd("Paste"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=paste(TA, r).
	
cmd("Select All"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=selectAll(TA, r).
	
cmd("to Upper Case"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=replaceSelection(getSelectedText(TA, Text), UpperCaseText, r),
	gc=toUpperCase(Text, UpperCaseText).
	
cmd("to Lower Case"), tab(Tab) :-
	tab=getSelectedComponent(Tab, Scroll),
	gc=getViewport(Scroll, Viewport), gc=getView(Viewport, TA),
	gc=replaceSelection(getSelectedText(TA, Text), LowerCaseText, r),
	gc=toLowerCase(Text, LowerCaseText).
	
cmd("Exit"), frame(Frame), tab(Tab) :- gc(Frame), gc(Tab), r=java_lang_System.exit(0).

/////////////////////////////////////////////
// GC

gc(Object) :- class(Object, "java.lang.Object") | .
r(done) :- .