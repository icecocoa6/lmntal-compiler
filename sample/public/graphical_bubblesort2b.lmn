/**
  Coyright(C) 2006 Ueda Laboratory LMNtal Group, All rights reserved.
  $Author: nakano $

  SYNOPSIS
	bubblesort of random elements
        uses integer and graphic libraries API
*/
graphic.use.
go :- 
{ window, position(0,0), size(500,500),
  name("window"), timer(20), killer,
  gen(45).
  gen(N) :- N = 0 | done.
  gen(N) :- N > 0 |
     gen(N-1),
     { name(N), position(0,(N-1)*10),
       size((integer.rnd(44)+1)*10,10),
       draw, getpic("fillrect") }.
  done :- 
    ({name(_IN),size(_IS,10),$p,@p},
     {name(_IN2),size(_IS2,10),$q,@q} :-
	   _IN = _IN2 - 1  ,_IS > _IS2 |
       {name(_IN),size(_IS2,10),$p},
       {name(_IN2),size(_IS,10),$q}).
}.

