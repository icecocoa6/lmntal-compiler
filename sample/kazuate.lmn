/*
 * 数当てゲーム  by INUI
 * コンピュータがランダムに決定した1~100の整数値を当てる
 * 入力した数が大きいか小さいかが、ヒントとして表示される
 * 
 * special thanks: SAKURAI, KUDO
 */
{
	io.use.

	//この数を当てる
	ans(integer.rnd(100)).

	//制限回数
	count(6).
	
	//制御用のdoneアトムを作る
	io.popup("kazuate game start!").

	done, count(X) :- X > 0 | ret = io.inputInteger([:number?:]), count(X-1).

	ans(X), ret(Y) :- X=Y | ok.
	ans(X), ret(Y) :- X<Y | ans(X), io.popup("too big!").
	ans(X), ret(Y) :- X>Y | ans(X), io.popup("too small!").

	//doneがないと最後に正解したときにもこのルールが適用されてしまう
	done, count(X) :- X = 0 | io.popup("game over.").
	
	ok, count(X) :- io.popup("Congratulations!"), X=0.
}.

//上の膜の中に書くとio.popupでdoneができてしまい、ゲームが終わらない
//{ok, $p[], @p}/ :- io.popup("Congratulations!").