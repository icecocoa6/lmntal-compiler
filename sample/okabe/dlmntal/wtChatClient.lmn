% 2007/05/28
% @see wtChatServer.lmn

{
  sys.perpetual(on).
  window.
  name("LMNchat Client").
  size(400,300).
  killer.

  myname{ textarea, text(""), position(0,0), size(1,1), weight(1,0) }.
  login{ button, text("login"), position(1,0) }.
  
  dlmntal.connect("127.0.0.1",50000,[],msg).
  
  login@@
  login{ clicked, $l[],@l }, myname{ text(_Sname), $n[],@n } :- 
  myname(string.concat(_Sname,"> ")),
  screen{ textarea, text(""), position(0,0), size(2,1), weight(0, 1) },
  line{ textarea, text(""), position(0,1), size(1,1), weight(1, 0) },
  submit{ button, text("submit"), position(1,1) }.
  
  submit@@
  myname(_Sname), submit{ clicked, $s,@s }, line{ text(_Sline), $l,@l },
  dlmntal.socket(Send,Recv,Obj) :-
  myname(_Sname),
  submit{ $s,@s },
  line{ text(""), $l,@l },
  dlmntal.socket(list.append(Send,[string.concat(_Sname,_Sline)]),Recv,Obj).
  
  recv@@
  msg=[H|T], screen{ text(_Stext), $t,@t } :- string(H) |
  msg=T, screen{ text(string.concat(_Stext,string.concat(H,"\n"))), $t,@t }.
}.
