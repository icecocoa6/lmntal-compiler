/*
 * 最大値発見問題（アルゴリズムCR）
 * 2007/06/09(Sat) by okabe
 *
 * 相異なる自然数をもつノードが単方向リングを構成する
 * 
 * 参考：『分散アルゴリズム』（近代科学社）
 */

% 初期状態（Nを指定してルールで作りたい…）
Res=[node(ready,2,0),node(start,3,0),node(ready,1,0),node(ready,5,0),node(ready,4,0)|Res].

% 始動プロセス：自分のIDを送信する
start@@
Res=[node(start,Id,Max)|T] :- int(Id), int(Max), NewMax=Id |
	Res=[NewMax,node(run,Id,NewMax)|T].

% 始動プロセス以外のプロセス
ready@@
Res=[node(ready,Id,Max),M|T] :- Id>M, NewMax=Id, int(Max) |
	Res=[NewMax,node(run,Id,NewMax)|T].
ready@@
Res=[node(ready,Id,Max),M|T] :- Id<M, NewMax=M, int(Max) |
	Res=[NewMax,node(run,Id,NewMax)|T].

% メッセージ処理
recv@@
Res=[node(run,Id,Max),M|T] :- Max<M, NewMax=M, Id=\=M |
	Res=[NewMax,node(run,Id,NewMax)|T].
recv@@
Res=[node(run,Id,Max),M|T] :- Max>=M, Id=\=M |
	Res=[node(run,Id,Max)|T].

halt@@
Res=[node(run,Id,Max),M|T] :- Id=:=M, int(Max) |
	Res=[max(Max),node(Id,Max)|T].

recv_halt@@
Res=[node(run,Id,Max),max(M)|T] :-
	Res=[max(M),node(Id,Max)|T].
