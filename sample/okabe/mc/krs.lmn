/*
 * 最大値発見問題（アルゴリズムKRS）
 * 2007/06/17(Sun) by okabe
 * 
 * 相異なる自然数をもつノードが方向感覚のない双方向リングを構成する
 * コードが長いのは大小関係の比較でルールが２本になってしまうため（カスタムガードを使えばまとめられる）
 * 
 * 参考：『分散アルゴリズム』（近代科学社）
 */

% 初期状態
{node(1,0), start,
	out(1,[],X14), in(1,msg,X41), out(0,[],X12), in(0,msg,X21)},
{node(2,0), ready,
	out(0,[],X21), in(0,msg,X12), out(1,[],X23), in(1,msg,X32)},
{node(3,0), ready,
	out(0,[],X32), in(0,msg,X23), out(1,[],X34), in(1,msg,X43)},
{node(4,0), ready,
	out(0,[],X43), in(0,msg,X34), out(1,[],X41), in(1,msg,X14)}.
	
% 通信処理
channel@@
{out(D1,[H|T],X),$p[|*Y1],@p}, {in(D2,msg,X),$q[|*Y2],@q} :-
	unary(D1),unary(D2),ground(H),ground(T) |
	{out(D1,T,X),$p[|*Y1],@p}, {in(D2,M,X),msg([H|M]),$q[|*Y2],@q}.
	
% 始動プロセス
start@@
{start,node(Id,Max),out(1,[],X),$p[X|*Y],@p} :- int(Id),int(Max) |
	{run,node(Id,Id),out(1,[Id],X),$p[X|*Y],@p}.

% 始動プロセス以外のプロセス
ready@@
{ready,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([M|R]),$p[X1,X2|*Y],@p} :-
	Id>M,int(Max),D1=\=D2,ground(S) |
	{run,node(Id,Id),out(D1,list.append(S,[Id]),X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.
ready@@
{ready,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([M|R]),$p[X1,X2|*Y],@p} :-
	Id<M,int(Max),D1=\=D2,ground(S) |
	{run,node(Id,M),out(D1,list.append(S,[M]),X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.

% メッセージ処理
halt@@
{run,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([M|R]),$p[X1,X2|*Y],@p} :-
	Id=:=M,int(Max),D1=\=D2,ground(S) |
	{node(Id,Max),out(D1,list.append(S,[max(Max)]),X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.
recv_halt@@
{run,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([max(M)|R]),$p[X1,X2|*Y],@p} :-
	int(Id),int(Max),int(M),D1=\=D2,ground(S) |
	{node(Id,M),out(D1,list.append(S,[max(M)]),X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.
recv@@
{run,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([M|R]),$p[X1,X2|*Y],@p} :-
	M>Max,int(Id),D1=\=D2,ground(S) |
	{run,node(Id,M),out(D1,list.append(S,[M]),X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.
recv@@
{run,node(Id,Max),out(D1,S,X1),in(D2,R,X2),msg([M|R]),$p[X1,X2|*Y],@p} :-
	M=<Max,int(Id),D1=\=D2,ground(S) |
	{run,node(Id,Max),out(D1,S,X1),in(D2,msg,X2),$p[X1,X2|*Y],@p}.
	