/*

NAME

	A reader-writer lock

SYNOPSIS

AUTHOR

	Ryo Okabe

HISTORY

	2005/10/15(Sat)

*/

% A reader-writer lock
% reader と writer は同時にアクセスできない
% 複数の reader
% １つの writer

% 非同期通信のサンプル

% 凡例
%    bi(buffer-in),wo(writer-out) : 無通信状態
% -> bi1,wo1                      : データ送信準備（wo 側にデータが存在）
% -> bi2,wo                       : データ送信完了（bi 側にデータが存在）
% -> bi,wo                        : 無通信状態

% state=-1 書き込み中
% state=0  アイドル状態
% state>=0 Ｎ人が読み込み中

% 開始
start.

% 初期化
start :-
writer={wo(X),write=[4,5,6],write=[1,2]},
buffer={bi(X),state=0,data=[3]},
addReader(10).

addReader(N),buffer={$b,@b} :- N>0 |
addReader(N-1),reader={ri(X),read,read},buffer={bo(X),$b,@b}.
addReader(N) :- N=0 | ().

% 書き込み準備
{write(D),wo(X),$w,@w},{bi(X),state(S),$b,@b} :- ground(D),S=:=0 |
{wo1(X,D),$w,@w},{bi1(X),state(-1),$b,@b}.

% 書き込み
{wo1(X,D),$w,@w},{bi1(X),$b,@b} :- ground(D) |
{wo(X),$w,@w},{bi2(X,D),$b,@b}.

% データの更新
{bi2(B,D),data(O),state(S),$b,@b} :- ground(D),ground(O),int(S) |
{bi(B),data(D),state(0),$b,@b}.

% 読み込み準備 
{read,ri(X),$r,@r},{bo(X),data(D),state(S),$b,@b} :- ground(D),S>=0 |
{ri1(X),$r,@r},{bo1(X,D),data(D),state(S+1),$b,@b}.

% 読み込み
{ri1(X),$r,@r},{bo1(X,D),state(S),$b,@b} :- ground(D),int(S) |
{ri2(X,D),$r,@r},{bo(X),state(S-1),$b,@b}.

% データの取得
{ri2(X,D),$r,@r} :- ground(D) |
{ri(X),data(D),$r,@r}.
