/*

	nakano ---
	跳ねるボール。
	オプション　--graphic をつけて実行すること。
	このプログラムは無限ループします。
	
	ライブラリintegerを使用しています。

*/

{
draw.
name("rect").
getpic("rect").
position(60,60).
sequence(3).
enable.
size(340,340).
}.

{
a.
draw.
name("hoge").
getpic("circle").
position(200,150).
sequence(2).
enable.
size(50,50).
vx(5).
vy(5).
}.

{
b.
draw.
name("moge").
getpic("circle").
position(80,15).
sequence(1).
enable.
size(50,50).
vx(5).
vy(5).
}.
c.

{a,position(X,Y),vx(VX),vy(VY), $p , @p},c :-  VX > 0 , VY > 0 , X < 350 ,Y < 350 | {a,position( X + VX, Y + VY),vx(VX),vy(VY),$p},d.
{a,position(X,Y),vx(VX),vy(VY), $p , @p},c :-  VX < 0 , VY > 0 , X > 60 ,Y < 350  | {a,position( X + VX, Y + VY),vx(VX),vy(VY),$p},d.
{a,position(X,Y),vx(VX),vy(VY), $p , @p},c :-  VX > 0 , VY < 0 , X < 350 , Y > 60 | {a,position( X + VX, Y + VY),vx(VX),vy(VY),$p},d.
{a,position(X,Y),vx(VX),vy(VY), $p , @p},c :-  VX < 0 , VY < 0 , X > 60 , Y > 60  | {a,position( X + VX, Y + VY),vx(VX),vy(VY),$p},d.

{position(X,Y),vx(VX), $p , @p} :-  VX > 0 , X >= 350, int(Y) | {position( X , Y ),vx(-1 * integer.rnd(15) - 1),$p}.
{position(X,Y),vx(VX), $p , @p} :-  VX < 0 , X =< 60, int(Y)  | {position( X , Y ),vx(integer.rnd(15) + 1),$p}.
{position(X,Y),vy(VY), $p , @p} :-  VY > 0 , Y >= 350, int(X) | {position( X , Y ),vy(-1 * integer.rnd(15) - 1),$p}.
{position(X,Y),vy(VY), $p , @p} :-  VY < 0 , Y =< 60, int(X)  | {position( X , Y ),vy(integer.rnd(15) + 1),$p}.

{b,position(X,Y),vx(VX),vy(VY), $p , @p},d :-  VX > 0 , VY > 0 , X < 350 ,Y < 350 | {b,position( X + VX, Y + VY),vx(VX),vy(VY),$p},c.
{b,position(X,Y),vx(VX),vy(VY), $p , @p},d :-  VX < 0 , VY > 0 , X > 60 ,Y < 350  | {b,position( X + VX, Y + VY),vx(VX),vy(VY),$p},c.
{b,position(X,Y),vx(VX),vy(VY), $p , @p},d :-  VX > 0 , VY < 0 , X < 350 , Y > 60 | {b,position( X + VX, Y + VY),vx(VX),vy(VY),$p},c.
{b,position(X,Y),vx(VX),vy(VY), $p , @p},d :-  VX < 0 , VY < 0 , X > 60 , Y > 60  | {b,position( X + VX, Y + VY),vx(VX),vy(VY),$p},c.
